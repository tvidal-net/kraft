buildscript {
    ext {
        kotlinVersion = '1.3.10'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    group 'uk.tvidal'
    version '0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = project.targetCompatibility
            allWarningsAsErrors = true
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.testng:testng:$testngVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Vendor': project.group,
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version
            )
        }
    }
}
